export ZSH=$HOME/.local/src/oh-my-zsh
# Moving custom Oh My Zsh files out of $ZSH to avoid weirdness with manually
# edited files within a Chezmoi external directory.
ZSH_CUSTOM=$HOME/.local/src/oh-my-zsh-custom
export PATH=$HOME/bin:$HOME/.local/bin:$PATH

# History settings
HIST_STAMPS="yyyy-mm-dd"
HISTSIZE=1000000
SAVEHIST=1000000
setopt APPEND_HISTORY

# Oh My Zsh settings
zstyle ':omz:update' mode disabled
COMPLETION_WAITING_DOTS="true"
ZSH_THEME="daniel15"
ZSH_TMUX_AUTOQUIT="false"
plugins=(
    fzf
    git
    mercurial
    zsh-autosuggestions
    # This one needs to go last
    zsh-syntax-highlighting
)

source $ZSH/oh-my-zsh.sh

# Determine environment
if [[ $HOSTNAME == *.fbinfra.net || $HOSTNAME == *.facebook.com ]]; then
    export DANIEL15_ENV='work-devserver'
elif [[ $HOSTNAME == danlo-fedora-* ]]; then
    export DANIEL15_ENV='work-client'
# TODO: Add personal servers
else
    export DANIEL15_ENV='home'
fi

# Configure EDITOR
if [[ $TERM_PROGRAM == 'vscode' ]]; then
    # Terminal in VS Code - Should open file in VS Code to edit
    if [[ $DANIEL15_ENV == 'work-devserver' || $DANIEL15_ENV == 'work-client' ]]; then
        export EDITOR='code-fb --wait'
    else
        export EDITOR='code --wait'
    fi
elif [[ -n $DISPLAY ]]; then
    # GUI terminal, NOT in VS Code
    export EDITOR=kate
else
    # SSH session, TTY, etc.
    export EDITOR=nano
fi

# Shim for environments where `_dotfiles_scm_info` is not available (non-work)
function _dotfiles_scm_info {}

# FB-specific initialization
[[ -f /usr/facebook/ops/rc/master.zshrc ]] && source /usr/facebook/ops/rc/master.zshrc
[[ -f /usr/share/scm/scm-prompt.sh ]] && source /usr/share/scm/scm-prompt.sh
if [[ $DANIEL15_ENV == 'work-devserver' ]]; then
    alias curl='curl $(fwdproxy-config curl)'
    alias wget='wget $(fwdproxy-config wget)'
    alias chezmoi='HTTPS_PROXY=http://fwdproxy:8080 chezmoi'

    function proxy_on() {
        export HTTP_PROXY=http://fwdproxy:8080
        export HTTPS_PROXY=http://fwdproxy:8080
    }

    function proxy_off() {
        export HTTP_PROXY=
        export HTTPS_PROXY=
    }
fi

source $ZDOTDIR/xdgify.sh
source $ZDOTDIR/aliases.sh

if [[ -o interactive ]] && (( $+commands[fastfetch] )); then
    fastfetch
fi
